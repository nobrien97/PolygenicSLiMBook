/* Quantitative simulation for a single polygenic trait
 (C) Nick O'Brien and OB Lab, University of Queensland, Australia
 Here we consider a single trait evolving through time with a wealth of parameters to play with.
 Options exist for drift, stabilising selection, and directional selection
*/


// set up a simple neutral simulation
initialize() {
	if (!exists("slimgui")) {
		setCfgParam("seed", getSeed());
	}
	else {
		setSeed(asInteger(round(runif(1, 1, 2^62 - 1))));
		setCfgParam("seed", getSeed());
		catn("Actual seed: " + asInteger(seed));
	}
	
	
	// Declare/define parameters if they haven't been by the command line arguments
	// Values from literature are cited
	// The realistic range of w^2 is 3-50, so 18 to 5000 for 2w^2, or 0.05556 to 0.0002 for 1/2w^2
	setCfgParam("mu", 8.045e-6); // Mutation rate averaged across five species from Table 1 Aston et al. 2017 
	setCfgParam("Ne", 1000 ); // Population size
	setCfgParam("del_mean", -1); // Mean used for the gamma distribution that deleterious mutations are pulled from
	setCfgParam("del_shape", 10); // Shape parameter for gamma distribution used for deleterious mutations
	setCfgParam("mutWeights", c(5, 10, 1)); // Relative proportions of Neutral/Deleterious/QTL mutations occurring at each site.
	setCfgParam("rwide", 1.241e-4); // Whole genome recombination rate (per locus)
	setCfgParam("nloci", 100); // Number of QTL positions along the chromosome - where QTL mutations can appear, sampled from sample(): Fixed at 100
	setCfgParam("genomelength", 100); // Loci across the genome - some are deleterious, others QTL, others neutral
	setCfgParam("locimu", 0.0); // Mean used for QTL mutation effect distribution pulls
	setCfgParam("locisigma", 1.0); // Variance used for QTL effect pulls - the additive effect size distribution
	setCfgParam("locidist", "n"); // Distribution used for locus distribution. "n" = normal, "g" = gamma
	setCfgParam("width", 0.005); // Width of the fitness curve: smaller = wider curve, lower selection pressure: need to be careful with values, they need to be relative to the mean phenotype after burn-in, and the initial distance from the optimum. This should be precalculated as 1/2w^2, as in Lande 1976. Only valid for stabilising selection models
	setCfgParam("opt", 30); // How far away optimum phenotype should be from mean phenotype post-burnin: Kingsolver et al. 2001 suggests .01 to 10 phenotypic units
	setCfgParam("selType", "s"); // Which type of selection to use: "s" = stabilising, "p" = positive/directional, "d" = genetic drift
	setCfgParam("printH", F); // Print values of heterozygosity over time during burn-in: used to decide how long burn-in needs to be
	setCfgParam("burnTime", 2500); // Number of generations of neutral burn-in before the test period
	setCfgParam("stabTime", 1000); // Number of generations of stabilising selection around the burn-in phenotype before the optimum shifts
	setCfgParam("testTime", 10000); // Number of generations of test time: where selection is applied (or additional generations of drift, if selType = "d")
	setCfgParam("samplerate", c(500, 2500)); // Sample rate in generations for phenotypic output (first value) and allelic output (second value)
	setCfgParam("adaptiveSampling", F); // Enable adaptive sampling rates to sample more often when phenotypes are changing rapidly. 
	setCfgParam("sampleLimits", c(0.1*samplerate, 5*samplerate)); // Set maximum and minimum sample rates to adjust between 
	
	setCfgParam("outPositions", 'out_slim1T_pos.csv'); // Output filename/path for locus positions
	setCfgParam("outBurn", 'out_slim1T_burnin.csv'); // Output burn-in heterozygosities
	setCfgParam("outName", 'out_slim1T_means.csv'); // Output filename/path for the trait means/variance
	setCfgParam("outOpt", 'out_slim1T_opt.csv'); // Output filename/path for the trait optimum values for each run
	setCfgParam("outMuts", 'out_slim1T_muts.csv'); //Output filename/path for the mutation information
	
	setCfgParam("modelindex", 1); // Identifier for the combination of predictors used in Latin Hypercube: this is the row number in the lscombos.csv file
	
	initializeMutationRate(mu);
	
	initializeMutationType("m1", 0.5, "f", 0.0); // Neutral, non-trait-affecting mutations: also a place-holder for all mutation types before we set them
	
	//m2 mutation type: deleterious/background selection
	initializeMutationType("m2", 0.5, "g", del_mean, del_shape);  // del_mean and del_shape are defined in the batch run script, or the cfg parameters
	m2.color = "red";
	
	//m3 mutation type: QTL mutation
	initializeMutationType("m3", 0.5, locidist, locimu, locisigma);
	m3.color = "green";
	
	
	// g1 genomic element type: uses m1 for all mutations
	initializeGenomicElementType("g1", m1, 1.0);
	
	// uniform chromosome of length 100 kb with uniform recombination
	initializeGenomicElement(g1, 0, genomelength-1);
	initializeRecombinationRate(rwide);
	
	
	
}


// Function definitions

function (void) setCfgParam(s$ name, ifls value) {
	// Automate defining constants for SLiMgui and slim command line - if we haven't defined it in the command line, set it here

	if (!exists(name)) {
		defineConstant(name, value);
	}
}


function (void) calcPhenotype(o<Individual> inds, o<Substitution> fixed) {
	// Calculate phenotypes for individuals based on their QTL effects - both segregating and substitutions

	sizem3 = size(fixed[fixed.mutationType == m3]);
	fixedfx = sizem3 ? sum(fixed[fixed.mutationType == m3].selectionCoeff) else 0.0;
	
	for (ind in inds) {
		muts = ind.genomes.mutationsOfType(m3);			// Get only trait effects
		mutscount = size(muts);
		
		phenotype = (mutscount ? sum(muts.selectionCoeff) else 0.0);
		phenotype = phenotype + fixedfx; // add fixed effects
		ind.setValue("phenotype", phenotype);
	}
}

function (void) calcFitnessStab(o<Individual> inds, fi$ optimum) {
	// Calculate fitness from phenotype. Make sure this is only called after CalcPhenotype() and after sim.setValue("optimum")!
	// We choose the fitness function based on the cfgParam selType, which activates/deactivates script blocks
	phenotypes = inds.getValue("phenotype"); 
	dists = (phenotypes - optimum)^2;
	// https://www.desmos.com/calculator/yelcak6wfo
	inds.fitnessScaling = 0.01 + exp(-(dists * width)); // Fitness function comes from Lande 1976: width is 1/2w^2
}


function (void) calcFitnessDir(o<Individual> inds) {
	// Calculate fitness from phenotype. Make sure this is only called after CalcPhenotype() and after sim.setValue("optimum")!
	// We choose the fitness function based on the cfgParam selType, which activates/deactivates script blocks
	phenotypes = inds.getValue("phenotype"); 
	inds.fitnessScaling = phenotypes;

}

function (float) lerp(fi xmin, fi xmax, fi ymin, fi ymax, fi x) {
	// Linear interpolation between two points
	return ymin + (x - xmin)*((ymax - ymin)/(xmax - xmin));
}


// create a population of Ne individuals
// Drosophila - deleterious mutation rate: Peter Keightley Eyre-Walker: with Ne estimate and genome size
// Good estimate of how many deleterious mutations occur per site and gen, and distribution of effects
1 {
	sim.addSubpop("p1", Ne);
	
	
	// Define locations of QTLs, deleterious loci, and neutral loci
	lengthvec = 0:(genomelength - 1);
	pos_QTL = sample(lengthvec, nloci); // Set the positions of our QTLs, from position 1 to the length of the chromosome;
	
	
	sim.setValue("pos_QTL", pos_QTL); //store these positions in a sim value
	
	posfile = paste(modelindex, asString(seed), nloci, pos_QTL, sep = ","); // Output information on where the QTLs are, and the recombination structure of the genome
	
	writeFile(outPositions, posfile, append = T);
	
	
	
	// Define constants for plotting heterozygosity (diagnostics)
	if (printH == T) {
		sim.setValue("h2_history", NULL);
		defineConstant("pngPath", writeTempFile("plot_", ".png", ""));
		if (exists("slimgui"))
			slimgui.openDocument(pngPath);
		}	
	

	
	
	// Activate burn-in time
	
	sim.rescheduleScriptBlock(s1, start = 1, end = burnTime);
	sim.rescheduleScriptBlock(s2, start = burnTime, end = (burnTime + stabTime + testTime));
	
	// Activate the right post-burnin script block depending on selType parameter: s1 = burnin s2 = mutation output s3 and s4, s5, s6 = stab sel, dir sel, drift
	if (selType == "p") {
		sim.deregisterScriptBlock(s4);
		sim.deregisterScriptBlock(s6);
		sim.rescheduleScriptBlock(s5, start = (burnTime + 1), end = (burnTime + testTime));
		}
	else if (selType == "d") {
		sim.deregisterScriptBlock(s4);
		sim.deregisterScriptBlock(s5);
		sim.rescheduleScriptBlock(s6, start = (burnTime + 1), end = (burnTime + testTime));

		}
	else {
		sim.deregisterScriptBlock(s5);
		sim.deregisterScriptBlock(s6);
		sim.rescheduleScriptBlock(s3, start = (burnTime + 1), end = (burnTime + stabTime));
		sim.rescheduleScriptBlock(s4, start = (burnTime + stabTime + 1), end = (burnTime + stabTime + testTime));
	}

	// Set delta difference value: for calculating change in fitness and phenotype
	sim.setValue("lastDist", 0.0);
	sim.setValue("lastw", 0.0);
	
	// Set initial sample rates for adaptive sampling
	if (adaptiveSampling == T) {
		sim.setValue("sRate", samplerate);
		sim.setValue("burnDelta", NULL);
		sim.setValue("curTime", 0);
	}	
	
}

// Mutations individually have no direct fitness effect, fitness is calculated on a trait basis
fitness(m3) {
	return 1.0;
}


// When a mutation appears, need to check if it occurs at a QTL and set the mutation type accordingly
mutation(m1) {

	if (any(mut.position == sim.getValue("pos_QTL"))) {
		choice = sample(sim.mutationTypes, 1, weights = mutWeights);
	}
	else {
		choice = sample(sim.mutationTypes[0:1], 1, weights = mutWeights[0:1]);
	}
	mut.setMutationType(choice);
	mut.setSelectionCoeff(mut.mutationType.drawSelectionCoefficient());	

	return T;
}

// Burn-in period
s1 2 late() {
	if (sim.generation % samplerate[0] != 0) // Grab a sample every 500 generations
		return;
	inds = sim.subpopulations.individuals;
	meanH = paste(calcHeterozygosity(inds.genomes), sep=",");
	VA = paste(calcVA(inds, sim.mutationTypes[2]));
	calcPhenotype(inds, sim.substitutions);
	phenomean = mean(inds.getValue("phenotype"));
	
	
	// Set the optimum as a fixed distance from post-burn-in population phenotype mean, and do our write to the standard phenomean output, rather than burn-in
	if (sim.generation == burnTime & selType == "s") {
		sim.setValue("optimum", (opt + phenomean));
		catn("Optimum is: " + (opt + phenomean) + "\nStarting stabilising selection regime");
		OptFile = paste(asString(seed), modelindex, opt, sep = ",");
		writeFile(outOpt, OptFile);
		
		w = mean(inds.fitnessScaling);
		dist = abs(phenomean - (opt + phenomean)); // Absolute values so that means aren't biased towards 0 by drift 
		Mfile = paste(sim.generation, asString(seed), modelindex, meanH, VA, phenomean, dist, w, sep=",");
		writeFile(outName, Mfile, append = T);
		
		sim.setValue("burninPheno", phenomean); // Set a temporary optimum
	
	}
	else {
		Bfile = paste(sim.generation, asString(seed), modelindex, meanH, VA, phenomean, sep=",");
		writeFile(outBurn, Bfile, append = T);
	}
	
	// Plotting heterozygosity: check operating system so we use the right R functions to draw the graph
	if (printH == T)  {
		sim.setValue("h2_history", c(sim.getValue("h2_history"), meanH));
		catn("Heterozygosity = " + meanH);
		y = sim.getValue("h2_history");
		bounds = c(min(asFloat(y)) - (0.1*min(asFloat(y))), max(asFloat(y)) + (0.1*max(asFloat(y))));

		rstr = paste('{',
					'x <- (1:' + size(y) + ') * ' + samplerate[0],
					'y <- c(' + paste(y, sep = ", ") + ')',
					'png(width = 4, height = 4, units = "in", res = 72, file = "' + pngPath + '")',
					'plot(x=x, y=y, xlim = c(0, ' + burnTime + '), ylim = c(' + paste(bounds, sep = ", ") + '), type = "l",',
						'xlab = "Generation", ylab = "Heterozygosity")',
					'box()',
					'dev.off()',
					'}', sep = "\n");
			scriptPath = writeTempFile("plot_", ".R", rstr);
			system("/usr/bin/Rscript", args = scriptPath);	
			deleteFile(scriptPath);

		}
}

///////////////////////////////////////////////////////////////////////////////////////////////////////	
// Allelic output: outputting effect sizes, types, frequencies etc.
///////////////////////////////////////////////////////////////////////////////////////////////////////

s2 2 late() {


	// Every 25000 generations: we can do it more often probably, but testing required	
	if (adaptiveSampling == F) {
		if (sim.generation % samplerate[1] != 0)
			return;
	} else {
		if (sim.generation % sim.getValue("sRate")[1] != 0)
			return;
	}
	
	// Store vectors of unique mutations, sorted by the generation in which they originated
	muts = sortBy(c(sim.mutations[sim.mutations.mutationType != m1]), "originGeneration");
	
	// same for substitutions
	subs = sortBy(c(sim.substitutions[sim.substitutions.mutationType != m1]), "originGeneration");
	
	dist = abs(mean(p1.individuals.getValue("phenotype")) - opt);
	
	// Set up an empty output to store all our mutations' information for this generation
	mutsLines = NULL;
	
	// Put information on these unique mutations into a separate mutations file	
	
	// Check if there are any substitutions, if not we need to fix the output lines	by not including the \n character at the end of the last mutation line. 
	// This is because writeFile() automatically adds a \n character, so we'd end up with an extra empty line if we do write that way
	
	genom = sim.subpopulations.genomes;
	
	for (mut in muts) {
		mutFreq = mean(genom.containsMutations(mut));
		mutCount = size(genom[genom.containsMutations(mut)]);
		mutType = mut.mutationType.id;
		mutValue = asString(mut.selectionCoeff);
		mutChi = mut.selectionCoeff * sqrt(1/(2*dist));
		
		// Removed if/else statements: This will result in blank lines in the output file, which we need to remove after the file is done being written with the command sed '/^$/d'
		// In a job array this might not work because the sed command will happen on each node, and when you glue them together there'll be no newline on the last entry,
		// so a couple will stick together. In that case, will have to run sed separately following the file being stuck together
		
		mutsLine = paste(sim.generation, asString(seed), modelindex, mutType, mut.id, mut.position, mut.originGeneration, mutValue, mutChi, mutFreq, mutCount, "NA", sep = ",");
		mutsLine = paste0(mutsLine, "\n");
		mutsLines = paste0(mutsLines, mutsLine);
	
	}
	
	
	subCount = Ne*2;
	for (sub in subs) {
		subFreq = 1.0;
		subType = sub.mutationType.id;
		subValue = asString(sub.selectionCoeff);
		subChi = sub.selectionCoeff * sqrt(1/(2*dist));
		
		subLine = paste(sim.generation, asString(seed), modelindex, subType, sub.id, sub.position, sub.originGeneration, subValue, subChi, subFreq, subCount, sub.fixationGeneration, sep = ",");
		subLine = paste0(subLine, "\n");
		
		mutsLines = paste0(mutsLines, subLine); // Add substitutions to mutation output
	
	}
	
	// Write the mutations file
	// Fail safe in case there are no mutations at some point and the for loop doesn't run
	if (exists('mutsLine') | exists('subLine'))
		writeFile(outMuts, mutsLines, append = T);


}



// Selection period

// Initial stabilising period around burn-in mean phenotype

s3 3 late() {
		// Calculate phenotypes + fitness, write output
	inds = sim.subpopulations.individuals;
	calcPhenotype(inds, sim.substitutions);
	calcFitnessStab(inds, sim.getValue("burninPheno"));
	
	if (sim.generation % samplerate[0] != 0) // Write output every x generations, given by the first value in samplerate
		return;
	
	meanH = paste(calcHeterozygosity(p1.genomes), sep=",");
	VA = paste(calcVA(p1.individuals, sim.mutationTypes[2]));
	phenotypes = inds.getValue("phenotype");
	phenomean = mean(phenotypes);
	w = mean(inds.fitnessScaling);
	dist = mean(abs(phenotypes - sim.getValue("burninPheno"))); // Absolute values so that means aren't biased towards 0 by drift 
	delta = dist - sim.getValue("lastDist");
	sim.setValue("lastDist", dist);
	sim.setValue("burnDelta", c(sim.getValue("burnDelta"), delta));
	Mfile = paste(sim.generation, asString(seed), modelindex, meanH, VA, phenomean, dist, w, delta, sep=",");
	writeFile(outName, Mfile, append = T);
	
	
	if (sim.generation == (burnTime + stabTime)) {
		catn("Shifting optimum...");
		if (adaptiveSampling == T) {
			burnDelta = sim.getValue("burnDelta");
			sim.setValue("burnDelta", mean(burnDelta)); // Average the change in phenotype for burn in period so we can differentiate from that
			sim.setValue("bigDelta", mean(burnDelta)); // Set an initial x1 value for lerp to characterise adaptation for adaptive sampling
		}

	}

}


// Stabilising selection

s4 3 late() {
	
	// Calculate phenotypes + fitness, write output
	inds = sim.subpopulations.individuals;
	calcPhenotype(inds, sim.substitutions);
	calcFitnessStab(inds, sim.getValue("optimum"));
	
	// End simulation if we're at the end of the test time
	if (sim.generation == (burnTime + stabTime + testTime)) {
		sim.simulationFinished();
	}
	
	
	if (adaptiveSampling == F) {
		if (sim.generation % samplerate[0] != 0) { // Write output every x generations, given by the first value in samplerate
			return;
		}
	} else {
		if ((sim.generation - sim.getValue("curTime")) % sim.getValue("sRate")[0] != 0)
			return;
	}
	meanH = paste(calcHeterozygosity(p1.genomes), sep=",");
	VA = paste(calcVA(p1.individuals, sim.mutationTypes[2]));
	phenotypes = inds.getValue("phenotype");
	phenomean = mean(phenotypes);
	w = mean(inds.fitnessScaling);
	dist = mean(abs(phenotypes - opt)); // Absolute values so that means aren't biased towards 0 by drift 
	deltaPheno = dist - sim.getValue("lastDist");
	sim.setValue("lastDist", dist);
	deltaw = w - sim.getValue("lastw");
	sim.setValue("lastw", w);
	Mfile = paste(sim.generation, asString(seed), modelindex, meanH, VA, phenomean, dist, w, deltaPheno, deltaw, sep=",");
	writeFile(outName, Mfile, append = T);
	
	
	if (adaptiveSampling == T) {
		// Compare rate of change to burn-in ROC and adjust sampling rate according to magnitude of the difference
		bigDelta = sim.getValue("bigDelta");
		if (deltaPheno > bigDelta) {
			bigDelta = deltaPheno;
			sim.setValue("bigDelta", bigDelta);
		}
		sim.setValue("sRate", asInteger(round(lerp(bigDelta, sim.getValue("burnDelta"), sampleLimits[0:1], sampleLimits[2:3], deltaPheno))));
		sim.setValue("curTime", sim.generation);
	}
	
	
	
}

// Directional selection

s5 3 late() {
		
	// Calculate phenotypes + fitness, write output
	inds = sim.subpopulations.individuals;
	calcPhenotype(inds, sim.substitutions);
	calcFitnessDir(inds);

	if (sim.generation % samplerate[0] == 0) { // Write output every x gens
		meanH = paste(calcHeterozygosity(p1.genomes), sep=",");
		VA = paste(calcVA(p1.individuals, sim.mutationTypes[2]));
		phenotypes = inds.getValue("phenotype");
		phenomean = mean(phenotypes);
		w = mean(inds.fitnessScaling);
		Mfile = paste(sim.generation, asString(seed), modelindex, meanH, VA, phenomean, w, sep=",");
		writeFile(outName, Mfile, append = T);
	}
	
}



// More drift instead of selection: still keeping track of phenotypes

s6 3 late() {
		
	// Calculate phenotypes + fitness, write output
	inds = sim.subpopulations.individuals;
	calcPhenotype(inds, sim.substitutions);

	if (sim.generation % samplerate[0] == 0) { // Write output every x gens
		meanH = paste(calcHeterozygosity(p1.genomes), sep=",");
		VA = paste(calcVA(p1.individuals, sim.mutationTypes[2]));
		phenotypes = inds.getValue("phenotype");
		phenomean = mean(phenotypes);
		Mfile = paste(sim.generation, asString(seed), modelindex, meanH, VA, phenomean, sep=",");
		writeFile(outName, Mfile, append = T);
	}
	
	
}


